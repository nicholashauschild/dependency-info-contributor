buildscript {
    ext {
        spring_boot_version = '1.5.6.RELEASE'
    }
    repositories {

    }

    dependencies {

    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "com.jfrog.artifactory" version "4.4.18"
}

apply plugin: 'java'
apply plugin: 'maven-publish'


/**
 * Property definitions
 */

group = 'com.github.nicholashauschild'
version = file('version.txt').text.trim()
sourceCompatibility = 1.8

def bintrayUsername = hasProperty('bintray_username') ? project.getProperty('bintray_username') : ''
def bintrayApiKey = hasProperty('bintray_apikey') ? project.getProperty('bintray_apikey') : ''
def isSnapshot = version.endsWith('SNAPSHOT')
def isRelease = !isSnapshot


/**
 * Dependencies
 */

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot', version: "${spring_boot_version}"
    compile group: 'org.springframework.boot', name: 'spring-boot-actuator', version: "${spring_boot_version}"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


/**
 * Wrapper configuration
 */

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}


/**
 * Artifact configuration
 */

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    jar
    archives sourcesJar
    archives javadocJar
}


/**
 * Publishing configuration
 */

def pomConfig = {
    name 'dependency-info-contributor'
    url 'https://github.com/nicholashauschild/dependency-info-contributor'
    licenses {
        license {
            name 'MIT License'
            url 'https://opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'nicholashauschild'
            name 'Nicholas Hauschild'
            email 'nicholas.hauschild@gmail.com'
        }
    }
}

publishing {
    publications {
        theJar(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'SpringBoot InfoContributor that provides gradle dependency details')
                root.children().last() + pomConfig
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUsername
            password = bintrayApiKey
        }
        defaults {
            publications 'theJar'
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey

    publications = ['theJar']
    publish = true

    pkg {
        repo = 'maven'
        name = 'dependency-info-contributor'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/nicholashauschild/dependency-info-contributor.git'

        desc = 'SpringBoot InfoContributor that provides gradle dependency details'
        labels = ['spring-boot', 'info-contributor', 'java']
        websiteUrl = 'https://github.com/nicholashauschild/dependency-info-contributor'
        issueTrackerUrl = 'https://github.com/nicholashauschild/dependency-info-contributor/issues'
    }
}
